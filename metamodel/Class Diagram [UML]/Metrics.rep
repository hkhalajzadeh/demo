Report 'Metrics'
/* Metrics report reads model hierarchy and reports number of objects and relationships per graph */ 
subreport '_translators' run
/* Initialize the variable for counting whether the graph is already reported for cases of recursive model hierarchies */
$Graphs=' '
$num_graphs=$total_properties=$total_used_properties=$total_empty_properties='0'
subreport '_GraphMetrics' run 
newline
'METRICS:' newline
'Total number of reported graphs: ' $num_graphs newline
'Total number of objects: ' math $total_objects evaluate newline
'Total number of relationships: ' math $total_relationships evaluate newline
'Total number of connections: ' math $total_connections evaluate newline
'Total number of roles: ' math $total_roles evaluate newline
'Total number of properties: ' math $total_properties evaluate newline
'Total number of used properties: ' math $total_used_properties evaluate newline
'Total number of empty properties: ' math $total_empty_properties evaluate newline
newline
do __('Graph' newline 'Object' newline 'Relationship' newline 'Role')
{	@metaType = id
	'Total occurrences of each ' @metaType ' type:' newline
	do __(variable 'all_' @metaType '_types' read) 
	where id 
	orderby variable 'total_' id '_' @metaType read num desc, id
	{	'	' id ': ' variable 'total_' id '_' @metaType read newline 
	}
}
endreport